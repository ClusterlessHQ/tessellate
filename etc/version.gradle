/*
 * Copyright (c) 2018 Chris K Wensel <chris@wensel.net>. All Rights Reserved.
 *
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/.
 */

apply from: './etc/properties.gradle'

project.ext.currentCommit = System.properties[ 'build.vcs.number' ];

if( !currentCommit )
  {
  def commitPath = File.createTempFile( "commit", "tmp" )

  ant.exec( dir: '.', executable: "git", output: commitPath ) {
    arg( line: 'rev-parse HEAD' )
  }

  currentCommit = commitPath.readLines().get( 0 )

  commitPath.delete()
  }

def versionProperties = new Properties()
file( 'version.properties' ).withInputStream {versionProperties.load( it )}

ext.majorVersion = versionProperties[ "${rootProject.name}.release.major" ]
ext.minorVersion = versionProperties[ "${rootProject.name}.release.minor" ]

ext.buildNumber = System.getProperty( 'build.number', 'dev' ) // local dev or build server build

if( System.properties[ "${rootProject.name}.release.private" ] )
  buildNumber = "priv-${buildNumber}"
else if( !System.properties[ "${rootProject.name}.release.final" ] )
  buildNumber = "wip-${buildNumber}"

ext.releaseVersion = majorVersion

if( minorVersion )
  releaseVersion = "${majorVersion}.${minorVersion}"

if( !System.properties[ "${rootProject.name}.release.final" ] ) // is wip
  releaseVersion = "${releaseVersion}-${buildNumber}"

ext.releaseTag = "${releaseVersion}"

if( !System.properties[ 'build.number' ] ) // is dev
  releaseTag = "wip-${majorVersion}"

if( !System.properties[ "${rootProject.name}.release.final" ] ) // is wip
  releaseTag = "${majorVersion}-${buildNumber}"