/*
 * Copyright (c) 2017 Chris K Wensel <chris@wensel.net>. All Rights Reserved.
 *
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/.
 */

apply plugin: 'maven'

configurations {
  shaded
  shadedArchives
}

shadowJar {
  baseName = "${project.name}-shaded"
  classifier = ''
  mergeServiceFiles()
  configurations = []
  from {
    project.configurations.runtime.minus( [project.configurations.compileOnly] )
  }
  exclude 'LICENSE'
  exclude 'META-INF/*.DSA'
  exclude 'META-INF/LICENSE*'
}

uploadShadow {
  def deployer = repositories.mavenDeployer {
    configuration = configurations.shadow

    repository( url: repoUrl ) {
      authentication( userName: repoUserName, password: repoPassword )
    }

    pom.project {
      inceptionYear '2017'
      url 'http://www.cascding.org/'
    }
  }
  deployer.pom.whenConfigured {pom -> pom.dependencies = []}
}

task installShadowArchives( type: Upload ) {
  configuration = configurations[ 'shadedArchives' ]
  def installer = repositories.mavenInstaller {
    configuration = configurations.shadedArchives
    pom.project {
      inceptionYear '2017'
      url 'http://www.cascading.org/'
    }
  }
  installer.pom.whenConfigured {pom -> pom.dependencies = []}
}

artifacts {
  shadedArchives shadowJar
}

uploadArchives.dependsOn( uploadShadow )

install.dependsOn( installShadowArchives )
